plugins {
    id 'application'
}

group 'de.benkan'
version '1.0.0'

application {
    mainClass = 'de.benkan.processing.ProcessingApp'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    dependsOn(':data-models:jar', ':shared-kafka:jar')
    manifest {
        attributes(
                'Main-Class': 'de.benkan.processing.ProcessingApp',
        )
    }

    // include all dependencies as a fat jar
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':data-models')
    implementation project(':shared-kafka')

    implementation 'com.google.inject:guice:5.0.1'
    implementation 'io.dropwizard:dropwizard-core:2.0.25'
    implementation 'io.projectreactor:reactor-core:3.4.13'
    implementation 'javax.validation:validation-api:2.0.1.Final'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}